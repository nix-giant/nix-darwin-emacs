#+TITLE: nix-darwin-emacs

A nix overlay for bleeding edge and nearly stable Emacs on Darwin (the heart of macOS).

* About this overlay
** Emacs
This overlay provides packages =emacs-*= which is:
+ built from the emacs git repo directly
+ patched with [[https://github.com/d12frosted/homebrew-emacs-plus/tree/master/patches][patches from homebrew-emacs-plus]].
+ embellished by [[https://github.com/SavchenkoValeriy/emacs-icons][3D icon created by Valeriy Savchenko]].

** Emacs packages
This overlay doesn't provide any other Emacs packages, such as:
+ =melpaStablePackages.*=
+ =melpaPackages.*=
+ =elpaPackages.*=
+ ...

If you need them, try to use overlay ~package~ provided by [[https://github.com/nix-community/emacs-overlay][nix-community/emacs-overlay]].

** Binary cache
Binary cache is not available.

* Quick Start
First, build it:
#+begin_src sh
$ nix build .#emacs-29
$ nix build .#emacs-30
$ nix build .#emacs-unstable
#+end_src

#+begin_quote
This repo can't guarantee =emacs-unstable= will be built successfully.

This typically occurs because =emacs-unstable= updates very frequently, but the patches from =homebrew-emacs-plus= are incompatible with it. You could try syncing =overlays/unstable-patches= with the patches provided by =homebrew-emacs-plus/patches/emacs-31= (pull requests are welcome).

As I normally use =emacs-30=, I don't update =overlays/unstable-patches= very promptly. ;(
#+end_quote

Then, explore the directory =result=:
#+begin_src sh
$ ls ./result
#+end_src

* An example using Flake
#+begin_src nix
{
  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-<version>-darwin"; # replace <version>

    darwin = {
      url = "github:nix-darwin/nix-darwin";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    darwin-emacs = {
      url = "github:nix-giant/nix-darwin-emacs";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    darwin-emacs-packages = {
      url = "github:nix-community/emacs-overlay";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs =
    { self
    , nixpkgs
    , darwin
    , darwin-emacs
    , darwin-emacs-packages
    }: {
      darwinConfigurations = {
        "current" = darwin.lib.darwinSystem rec {
          system = "aarch64-darwin"; # or "x86_64-darwin"

          modules = [
            {
              nixpkgs = {
                overlays = [
                  # 1. use `emacs` overlay provided by this repo
                  darwin-emacs.overlays.emacs
                  # 2. use `package` overlay provided by nix-community/emacs-overlay
                  darwin-emacs-packages.overlays.package
                ];
              };
            }
          ];
        };
      };
    };
}
#+end_src

* Extra functions
This overlay comes with extra functions to generate an Emacs closure from various types of dependency declaration. (These are abstractions on top of =emacsWithPackages=.)

For example, =emacsWithPackagesFromUsePackage= adds packages which are required in a user's config via =use-package=.

#+begin_src nix
{ pkgs, ... }:
{
  environment.systemPackages = [
    (pkgs.emacsWithPackagesFromUsePackage {
      # Emacs config file.
      #
      # Supported formats:
      # + elisp source code - `*.el`
      # + org-mode babel files - `*.org`
      #
      # Note:
      # Config files cannot contain unicode characters, since they're being parsed in nix,
      # which lacks unicode support.
      #
      # elisp source code
      config = ./emacs.el;

      # or, org-mode babel files
      # config = ./emacs.org;

      # Whether to include your config as a default init file.
      # If being bool, the value of config is used.
      # Its value can also be a derivation like this if you want to do some
      # substitution:
      #   defaultInitFile = pkgs.substituteAll {
      #     name = "default.el";
      #     src = ./emacs.el;
      #     inherit (config.xdg) configHome dataHome;
      #   };
      defaultInitFile = true;

      # Package is optional, defaults to pkgs.emacs-unstable
      package = pkgs.emacs-29;

      # By default emacsWithPackagesFromUsePackage will only pull in
      # packages with `:ensure`, `:ensure t` or `:ensure <package name>`.
      # Setting `alwaysEnsure` to `true` emulates `use-package-always-ensure`
      # and pulls in all use-package references not explicitly disabled via
      # `:ensure nil` or `:disabled`.
      # Note that this is NOT recommended unless you've actually set
      # `use-package-always-ensure` to `t` in your config.
      alwaysEnsure = true;

      # By default emacsWithPackagesFromUsePackage will prefer to pull in
      # packages from MELPA.
      # Setting `alwaysPin` to `gnu` emulates `use-package-always-pin`
      # and pulls in all use-package references from GNU ELPA.
      # Note that this is NOT recommended unless you've actually set
      # `use-package-always-pin` to `"gnu"` in your config.
      alwaysPin = "gnu";

      # For Org mode babel files, by default only code blocks with
      # `:tangle yes` are considered. Setting `alwaysTangle` to `true`
      # will include all code blocks missing the `:tangle` argument,
      # defaulting it to `yes`.
      # Note that this is NOT recommended unless you have something like
      # `#+PROPERTY: header-args:emacs-lisp :tangle yes` in your config,
      # which defaults `:tangle` to `yes`.
      alwaysTangle = true;

      # Optionally provide extra packages not in the configuration file.
      # This can also include extra executables to be run by Emacs (linters,
      # language servers, formatters, etc)
      extraEmacsPackages = epkgs: [
        epkgs.cask
        pkgs.shellcheck
      ];

      # Optionally override derivations.
      override = final: prev // {
        weechat = prev.melpaPackages.weechat.overrideAttrs(old: {
          patches = [ ./weechat-el.patch ];
        });
      };
    })
  ];
}
#+END_SRC

Similarly, =emacsWithPackagesFromPackageRequires= adds packages which
are declared in a =.el= package file's =Package-Requires= header, which
can be handy for CI purposes:

#+BEGIN_SRC nix
# ...
let
  emacsForCI = pkgs.emacsWithPackagesFromPackageRequires {
    packageElisp = builtins.readFile ./flycheck.el;
    extraEmacsPackages = epkgs: [
      epkgs.package-lint
    ];
  };
pkgs.mkShell {
  buildInputs = [ emacsForCI ];
}
#+end_src

** About =:pin= keyword of =use-package=
As you know, =:pin= keyword of =use-package= pins a package to a specific archive. This repo has support of this feature. For example:

#+begin_src emacs-lisp
(use-package magit
  :ensure t
  :pin melpa-stable)
#+end_src

Above code will install =magit= from =melpa-stable= archive.

Following archives are supported:

- =gnu=, which refers to =epkgs.elpaPackages=
- =gnu-devel=, which refers to =epkgs.elpaDevelPackages=
- =nongnu=, which refers to =epkgs.nongnuPackages=
- =nongnu-devel=, which refers to =epkgs.nongnuDevelPackages=
- =melpa=, which refers to =epkgs.melpaPackages=
- =melpa-stable=, which refers to =epkgs.melpaStablePackages=

And, to avoid the errors of missing archives, it is better to set:

#+begin_src emacs-lisp
;; To prevent accidentally installing packages directly instead of through Nix,
;; nil is used as the addresses of archives.
(setq package-archives '(("gnu"          . nil)
                         ("gnu-devel"    . nil)
                         ("nongnu"       . nil)
                         ("nongnu-devel" . nil)
                         ("melpa"        . nil)
                         ("melpa-stable" . nil)))
#+end_src

* Testing

#+begin_src console
$ nix build '.#test-build-emacs'
#+end_src

* License
MIT
